# Maintainer: Andreas Brenk <mail@andreasbrenk.com>
pkgname=openhab-runtime
pkgver=1.2.0
pkgrel=3
pkgdesc="A universal integration platform for all things around home automation based on Java and OSGi"
url="https://code.google.com/p/openhab/"
arch=('x86_64')
license=('GPL3')
depends=('java-environment')
source=("https://openhab.googlecode.com/files/${pkgname}-${pkgver}.zip"
        'http://wrapper.tanukisoftware.com/download/3.5.19/wrapper-linux-x86-64-3.5.19.tar.gz'
        'openhab'
        'openhab.conf.d'
        'openhab.service'
        'wrapper.conf')
md5sums=('76f13fb6b2d23a96d0eb7df7ef6b32e4'
         '09f6865df57fa53ba0b76c97046bc691'
         '127ce75a8a82434bacc636204c84af94'
         'abe63d99dc7a173e079c6c033fd39377'
         '891947fb68de43849339ab0cd1b1a031'
         'dcd24c34feeda7c932aa201448d4b61d')
options=(!strip)
changelog=ChangeLog
install=install
backup=('opt/openhab/configurations/logback.xml'
        'opt/openhab/configurations/openhab.cfg'
        'opt/openhab/configurations/users.cfg')

package() {
  cd "$srcdir"

  install -d -m755 -o openhab -g openhab ${pkgdir}/opt/openhab
  install -d -m755 -o openhab -g openhab ${pkgdir}/opt/openhab/logs
  install -d -m755 -o openhab -g openhab ${pkgdir}/opt/openhab/run

  for dir in addons contexts etc server sounds webapps
  do
    find $dir -type d -exec install -d -m755 -o openhab -g openhab {} ${pkgdir}/opt/openhab/{} \;
    find $dir -type f -exec install -D -m644 -o openhab -g openhab {} ${pkgdir}/opt/openhab/{} \;
  done

  install -D -m644 -o openhab -g openhab configurations/logback.xml ${pkgdir}/opt/openhab/configurations/logback.xml
  install -D -m644 -o openhab -g openhab configurations/openhab_default.cfg ${pkgdir}/opt/openhab/configurations/openhab_default.cfg

  # install main configuration files group writable
  for dir in items persistence rules scripts sitemaps transform
  do
    find configurations/$dir -type d -exec install -d -m2775 -o openhab -g openhab {} ${pkgdir}/opt/openhab/{} \;
    find configurations/$dir -type f -exec install -D -m664 -o openhab -g openhab {} ${pkgdir}/opt/openhab/{} \;
  done

  # install sensitive files that contain API keys or passwords with restrictive permissions
  install -D -m600 -o openhab -g openhab configurations/openhab_default.cfg ${pkgdir}/opt/openhab/configurations/openhab.cfg
  install -D -m600 -o openhab -g openhab configurations/users.cfg ${pkgdir}/opt/openhab/configurations/users.cfg

  install -D -m755 -o openhab -g openhab openhab ${pkgdir}/opt/openhab/bin/openhab
  install -D -m644 -o openhab -g openhab wrapper.conf ${pkgdir}/opt/openhab/etc/wrapper.conf
  install -D -m644 -o openhab -g openhab openhab.conf.d ${pkgdir}/etc/conf.d/openhab
  install -D -m644 -o openhab -g openhab openhab.service ${pkgdir}/usr/lib/systemd/system/openhab.service

  cd "$srcdir"/wrapper-linux-x86-64-3.5.19

  install -D -m755 -o openhab -g openhab bin/wrapper ${pkgdir}/opt/openhab/lib/wrapper
  install -D -m644 -o openhab -g openhab lib/libwrapper.so ${pkgdir}/opt/openhab/lib/libwrapper.so
  install -D -m644 -o openhab -g openhab lib/wrapper.jar ${pkgdir}/opt/openhab/lib/wrapper.jar
}

# vim:set ts=2 sw=2 et:
